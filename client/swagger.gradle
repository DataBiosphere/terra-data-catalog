dependencies {
    // Version controlled by dependency management plugin
    implementation 'com.google.code.gson:gson'
}

def artifactGroup = "${group}.catalog"

openApiGenerate {
    inputSpec = "$rootDir/common/openapi.yml"
    generatorName = 'java'

    generateApiDocumentation = false
    generateApiTests = false
    generateModelDocumentation = false
    generateModelTests = false

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"
    invokerPackage = "${artifactGroup}.client"

    configOptions = [
        dateLibrary   : 'java11',
        java8         : 'true'
    ]
}

def openapiTargetDir = "${openApiGenerate.outputDir.get()}"

idea.module.generatedSourceDirs = [file("${openapiTargetDir}/src/main/java")]
sourceSets.main.java.srcDir "${openapiTargetDir}/src/main/java"

compileJava.dependsOn tasks.openApiGenerate
sourcesJar.dependsOn tasks.openApiGenerate
