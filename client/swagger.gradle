dependencies {
    // Version controlled by dependency management plugin
    implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'io.swagger:swagger-annotations:1.6.2'
}

def artifactGroup = "${group}.catalog"

openApiGenerate {
    inputSpec = "$rootDir/common/openapi.yml"
    generatorName = 'java'

    generateApiDocumentation = false
    generateApiTests = false
    generateModelDocumentation = false
    generateModelTests = false

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"
    invokerPackage = "${artifactGroup}.client"

    configOptions = [
        dateLibrary   : 'java11',
        java8         : 'true',
        library       : 'native'
    ]
}

String openapiTargetDir = "${openApiGenerate.outputDir.get()}/src/main/java"

idea.module.generatedSourceDirs = [file("${openapiTargetDir}")]
sourceSets.main.java.srcDir "${openapiTargetDir}"

compileJava.dependsOn tasks.openApiGenerate
sourcesJar.dependsOn tasks.openApiGenerate
