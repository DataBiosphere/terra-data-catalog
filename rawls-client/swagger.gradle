dependencies {
    // Version controlled by dependency management plugin
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.glassfish.jersey.core:jersey-client'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart'
}

task downloadRawlsYaml(type: Download) {
    // this was the latest git commit hash at development time
    src "https://raw.githubusercontent.com/broadinstitute/rawls/874b2e1bc6d95f90e1cf0db7569cfbaae9da34bf/core/src/main/resources/swagger/api-docs.yaml"
    dest "${buildDir}/rawls-api-docs.yaml"
}

def artifactGroup = "${group}.rawls"

openApiGenerate {
    inputSpec = "${buildDir}/rawls-api-docs.yaml"
    generatorName = "java"

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"
    invokerPackage = "${artifactGroup}.client"

    configOptions = [
        dateLibrary   : 'java11',
        java8         : 'true'
    ]
}

idea.module.generatedSourceDirs = [file("${openApiGenerate.outputDir}/main/src/main/java")]
sourceSets.main.java.srcDir "${openApiGenerate.outputDir}/main/src/main/java"
tasks.openApiGenerate.dependsOn tasks.downloadRawlsYaml
compileJava.dependsOn tasks.openApiGenerate
