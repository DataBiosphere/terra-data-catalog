dependencies {
    // Version controlled by dependency management plugin
    implementation 'com.google.code.gson:gson'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'io.swagger:swagger-annotations:1.6.2'
    implementation 'com.squareup.okio:okio:2.8.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'
    implementation 'io.gsonfire:gson-fire:1.8.4'
}

java {
    // Builds sources into the published package as part of the 'assemble' task.
    withSourcesJar()
}

task downloadRawlsYaml(type: Download) {
    // this was the latest git commit hash at development time
    src "https://raw.githubusercontent.com/broadinstitute/rawls/874b2e1bc6d95f90e1cf0db7569cfbaae9da34bf/core/src/main/resources/swagger/api-docs.yaml"
    dest "${buildDir}/rawls-api-docs.yaml"
}

def artifactGroup = "${group}.rawls"

openApiGenerate {
    inputSpec = "${buildDir}/rawls-api-docs.yaml"
    generatorName = "java"

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"
    invokerPackage = "${artifactGroup}.client"

    configOptions = [
        dateLibrary   : 'java11',
        java8         : 'true'
    ]
}

String openapiTargetDir = "${openApiGenerate.outputDir.get()}/src/main/java"

// get intellij to recognize openapi generated directory
idea.module.generatedSourceDirs = [file("${openapiTargetDir}")]
sourceSets.main.java.srcDir "${openapiTargetDir}"

tasks.openApiGenerate.dependsOn tasks.downloadRawlsYaml
compileJava.dependsOn tasks.openApiGenerate
sourcesJar.dependsOn tasks.openApiGenerate
