task downloadRawlsYaml(type: Download) {
    // this was the latest git commit hash at development time
    src "https://raw.githubusercontent.com/broadinstitute/rawls/874b2e1bc6d95f90e1cf0db7569cfbaae9da34bf/core/src/main/resources/swagger/api-docs.yaml"
    dest "${buildDir}/rawls-api-docs.yaml"
}

tasks.openApiGenerate.dependsOn tasks.downloadRawlsYaml

dependencies {
    // Version controlled by dependency management plugin
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.glassfish.jersey.core:jersey-client'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart'

    implementation 'com.github.scribejava:scribejava-core:8.3.1'
    implementation 'io.swagger.core.v3:swagger-annotations'
}

def artifactGroup = "${group}.rawls"

openApiGenerate {
    inputSpec = "${buildDir}/rawls-api-docs.yaml"
    generatorName = 'java'
    library = 'jersey2'

    generateApiDocumentation = false
    generateApiTests = false
    generateModelDocumentation = false
    generateModelTests = false

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"
    invokerPackage = "${artifactGroup}.client"

    configOptions = [
        dateLibrary: 'java11',
        java8      : 'true',
    ]
}

String openapiTargetDir = "${openApiGenerate.outputDir.get()}/src/main/java"

// get intellij to recognize openapi generated directory
idea.module.generatedSourceDirs = [file("${openapiTargetDir}")]
sourceSets.main.java.srcDir "${openapiTargetDir}"

compileJava.dependsOn tasks.openApiGenerate
