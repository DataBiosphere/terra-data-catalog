dependencies {
    // Version controlled by dependency management plugin
    implementation 'com.google.code.gson:gson'
}

task downloadRawlsYaml(type: Download) {
    // this was the latest git commit hash at development time
    src "https://raw.githubusercontent.com/broadinstitute/rawls/874b2e1bc6d95f90e1cf0db7569cfbaae9da34bf/core/src/main/resources/swagger/api-docs.yaml"
    dest "${buildDir}/rawls-api-docs.yaml"
}

def artifactGroup = "${group}.rawls"
def openapiTargetDir = "${buildDir}/generated"

openApiGenerate {
    inputSpec = "${buildDir}/rawls-api-docs.yaml"
    outputDir = "${buildDir}/generated"
    generatorName = "java"

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"
    invokerPackage = "${artifactGroup}.client"

    configOptions = [
        dateLibrary   : 'java11',
        java8         : 'true'
    ]
}

// get intellij to recognize openapi generated directory
idea.module.generatedSourceDirs = [ file("${openapiTargetDir}/src/main/java") ]
sourceSets.main.java.srcDir "${openapiTargetDir}/src/main/java"

tasks.openApiGenerate.dependsOn tasks.downloadRawlsYaml
compileJava.dependsOn tasks.openApiGenerate
