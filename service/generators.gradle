dependencies {
    runtimeOnly 'org.webjars.npm:swagger-ui-dist:4.9.0'

    // Versioned by Spring:
    implementation 'javax.validation:validation-api'
    implementation 'org.webjars:webjars-locator-core'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

def artifactGroup = "${group}.catalog"

openApiGenerate {
    inputSpec = "$rootDir/common/openapi.yml"
    generatorName = 'spring'

    generateApiDocumentation = false
    generateApiTests = false
    generateModelDocumentation = false
    generateModelTests = false

    apiPackage = "${artifactGroup}.api"
    modelPackage = "${artifactGroup}.model"

    /*
        interfaceOnly    : 'true',
        useTags          : 'true',
        springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot']

     */
    configOptions = [
        dateLibrary      : 'java11',
        java8            : 'true',
        interfaceOnly    : 'true',
        library : 'spring-boot',
        useTags : 'true',
    ]
}

def openapiTargetDir = "${openApiGenerate.outputDir.get()}"

sourceSets.main.java.srcDir "${openapiTargetDir}/src/main/java"
ideaModule.dependsOn tasks.openApiGenerate
compileJava.dependsOn tasks.openApiGenerate

// see https://github.com/n0mer/gradle-git-properties
gitProperties {
    keys = []
    customProperty('catalog.version.gitTag', { it.describe(tags: true) })
    customProperty('catalog.version.gitHash', { it.head().abbreviatedId })
    customProperty('catalog.version.github', { "https://github.com/DataBiosphere/terra-data-catalog/tree/${it.describe(tags: true)}" })
    customProperty('catalog.version.build', version)
}
