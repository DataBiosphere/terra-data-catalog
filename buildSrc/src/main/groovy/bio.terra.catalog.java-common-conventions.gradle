plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless'
    id 'com.github.spotbugs'
    id 'org.hidetake.swagger.generator'
}

boolean isCiServer = System.getenv().containsKey("CI")

if (!isCiServer) {
    tasks.withType(JavaExec).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
    tasks.withType(Test).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven {
        // Terra proxy for maven central
        url 'https://broadinstitute.jfrog.io/broadinstitute/maven-central/'
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.2'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.12'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.47'
    implementation 'org.slf4j:slf4j-api'
    implementation 'ch.qos.logback:logback-classic:1.4.14' //unpin when Spring Boot 3.2.1 is available
    implementation 'ch.qos.logback:logback-core:1.4.14' //unpin when Spring Boot 3.2.1 is available

    implementation 'bio.terra:terra-common-lib:0.1.9-SNAPSHOT'
    // guava is depended on by terra-common-lib, https://broadworkbench.atlassian.net/browse/DC-798
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'bio.terra:datarepo-jakarta-client:1.563.0-SNAPSHOT'
}

tasks.named('test') {
    useJUnitPlatform()
}

version = gradle.releaseVersion
group = 'bio.terra'

spotless {
    java {
        targetExclude "${buildDir}/**"
        targetExclude "**/swagger-code/**"
        googleJavaFormat()
    }
}

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (isCiServer) {
        dependsOn(spotlessCheck)
    } else {
        dependsOn(spotlessApply)
    }
}

// Spotbugs configuration
spotbugs {
    reportLevel = 'high'
    effort = 'max'
}
spotbugsMain {
    reports {
        if (isCiServer) {
            sarif.enabled = true
        } else {
            html.enabled = true
        }
    }
}

jacocoTestReport {
    reports {
        // sonar requires XML coverage output to upload coverage data
        xml.required = true
    }
}
